plugins {
    // Apply the Java Gradle plugin development plugin to add support for developing Gradle plugins
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
}

group = 'com.example'
version = '1.0'

ext {
    JAR_BASE_NAME = 'greeting-gradle-plugin'
}

repositories {
    mavenCentral()
}

dependencies {
    // Use the awesome Spock testing and specification framework
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
}

tasks.named('test') {
    // Use JUnit Jupiter for unit tests.
    useJUnitPlatform()
}

gradlePlugin {
    // Define the plugin
    plugins {
        MyGreeting {
            id = 'org.sample.Greetings'
            implementationClass = 'com.example.greeting.GreetingPlugin'
        }
    }
}

publishing {
    publications {
        Named(MavenPublication) {
            artifactId "${JAR_BASE_NAME}"
            from components.java
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir("repos-maven")
        }
    }
}

// We do not need to create the jar explicitly.
// The maven-publish plugin always create the jar with appropriate name automatically.
/*
jar {
    archiveBaseName = "${JAR_BASE_NAME}"
    archiveVersion = project.version
}
 */

// https://docs.gradle.org/current/userguide/publishing_gradle_module_metadata.html#sub:disabling-gmm-publication
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}
